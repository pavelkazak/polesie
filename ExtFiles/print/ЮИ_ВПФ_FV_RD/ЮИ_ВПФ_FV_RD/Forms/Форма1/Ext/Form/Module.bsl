
&НаСервере
Функция ВыполнитьНаСервере(ИмяМакета)
	МассивОбъектовНазначения = Ссылки.Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбъектыПечати = Новый СписокЗначений;
	ПараметрыВывода = УправлениеПечатью.ПодготовитьСтруктуруПараметровВывода();	
	КоллекцияПечатныхФорм= УправлениеПечатью.ПодготовитьКоллекциюПечатныхФорм(ИмяМакета);
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	ОбъектОбработка.Печать(МассивОбъектовНазначения, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, Истина);

	Возврат КоллекцияПечатныхФорм[0].ТабличныйДокумент;
	
КонецФункции

&НаКлиенте
Процедура Отладка(Команда)
	
	СписокКоманд = ПолучитьКоманды();
	Оповещение = Новый ОписаниеОповещения("ПоказатьВыборЭлементаЗавершение", ЭтотОбъект);
	СписокКоманд.ПоказатьВыборЭлемента(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВыборЭлементаЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабличныйДокумент = ВыполнитьНаСервере(ВыбранныйЭлемент.Значение);
	ТабличныйДокумент.Показать();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКоманды()
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	СведенияОВнешнейОбработке = ОбъектОбработка.СведенияОВнешнейОбработке();
	
	СписокКоманд = Новый СписокЗначений;
	Для Каждого СтрокаКоманды Из СведенияОВнешнейОбработке.Команды Цикл
		СписокКоманд.Добавить(СтрокаКоманды.Идентификатор, СтрокаКоманды.Представление);
	КонецЦикла;
	
	Возврат СписокКоманд;
		
КонецФункции
